plugins {
  id 'maven-publish'
  id 'java-library'
  id 'signing'
  id('io.github.gradle-nexus.publish-plugin') version '1.1.0'
}

group = 'io.camunda'
version = '1.1.1'
sourceCompatibility = '11'

repositories {
  mavenCentral()
}

dependencies {
  api "org.thymeleaf:thymeleaf:3.0.15.RELEASE"
  api "org.camunda.feel:feel-engine:1.15.1"
  
  testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

tasks.named('test') {
  useJUnitPlatform()
}

task sourceJar(type: Jar) {
  outputs.cacheIf { true }
  from sourceSets.main.allJava
  archiveClassifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  from javadoc.destinationDir
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
  archiveClassifier = 'javadoc'
}
java {
  withSourcesJar()
  withJavadocJar()
}

nexusPublishing {
  transitionCheckOptions {
    maxRetries.set(100)
    delayBetween.set(Duration.ofSeconds(10))
  }
  repositories {
    sonatype {
	  stagingProfileId = "13992b29744076"
	  nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
	  snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
	}
  }
}

signing {
    useInMemoryPgpKeys(System.getenv("ORG_GRADLE_PROJECT_signingKey"), System.getenv("ORG_GRADLE_PROJECT_signingPassword"))
    sign(publishing.publications)
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from(components.java)
      versionMapping {
         usage('java-api') {
             fromResolutionResult()
         }
      }
      pom {
        groupId = 'io.camunda'
        artifactId = 'thymeleaf-feel'
        packaging = 'jar'
        name = 'Thymeleaf FEEL'
        description = "Thymeleaf templating engine combined with FEEL expressions"
        url = "https://github.com/camunda-community-hub/thymeleaf-feel"
        licenses {
          license {
           name = "The Apache License, Version 2.0"
           url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
          }
        }
        developers {
          developer {
            name = "christophe.dame"
            email = "christophe.dame@camunda.com"
          }
        }
        scm {
          connection = "scm:git:git@github.com:camunda-community-hub/thymeleaf-feel.git"
          developerConnection = "scm:git:git@github.com:camunda-community-hub/thymeleaf-feel.git"
          url = "https://github.com/camunda-community-hub/thymeleaf-feel"
        }
      }
    }
  }
}

